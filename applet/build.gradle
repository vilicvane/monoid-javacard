group 'test-applet'
version '1.0-SNAPSHOT'

// Buildscript configuration for the javacard-gradle plugin.
// Do not modify this particular block. Dependencies for the project are lower.
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://javacard.pro/maven" }
        maven { url "https://deadcode.me/mvn" }
        // mavenLocal()
    }
    dependencies {
        classpath 'com.klinec:gradle-javacard:1.8.0'
    }
}

apply plugin: 'com.klinec.gradle.javacard'
apply plugin: 'idea'
apply plugin: 'application'
sourceCompatibility = 1.8

// Common settings, definitions
final def rootPath = rootDir.absolutePath
final def libs = rootPath + '/libs'
final def libsSdk = rootPath + '/libs-sdks'

// Repositories for your project
repositories {
    mavenCentral()
    // mavenLocal() // for local maven repository if needed

    // Repository with JCardSim, Globalplatform, etc, ...
    maven { url "https://javacard.pro/maven" }
    maven { url "https://deadcode.me/mvn" }

    // Local lib repository
    flatDir {
        dirs libs
    }
}

// Dependencies for your project
dependencies {
    jcardsim 'com.klinec:jcardsim:3.0.5.11'

    implementation 'com.klinec:jcardsim:3.0.5.11'

    testImplementation 'org.testng:testng:6.1.1'
    testImplementation 'org.slf4j:slf4j-api:1.7.33'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.33'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.17.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.1'

    testImplementation(group: 'com.klinec', name: 'javacard-tools', version: '1.0.4') {
        exclude group: "com.klinec", module: "jcardsim"
    }

    // Include plugin as it has bundled GP & other tools.
    // Alternative: include GP manually, but the included
    // version has to be compatible with the plugin.
    runtimeOnly 'com.klinec:gradle-javacard:1.8.0'
}

task dumpClassPath(dependsOn: ['idea']) {
    doLast {
        println "Gradle classpath:\n- " + configurations.compile.files*.name.join("\n- ")
        println "-------\n"
        println "IDEA classpath: \n- " + file(project.name + ".iml").readLines()
                .grep(~/.*"jar:.*/).collect { it.split("[\\/]")[-3].trim() }.join("\n- ")
        println "-------\n"
    }
}

application {
    mainClass = 'main.Run'
}

test {
    // useTestNG()
    useJUnitPlatform {
        excludeTags 'manual'
    }

    task manualTests(type: Test) {
        useJUnitPlatform {
            includeTags 'manual'
        }
        check.dependsOn it
        shouldRunAfter test
    }
}

// JavaCard SDKs and libraries
final def JC212 = libsSdk + '/jc212_kit'
final def JC221 = libsSdk + '/jc221_kit'
final def JC222 = libsSdk + '/jc222_kit'
final def JC303 = libsSdk + '/jc303_kit'
final def JC304 = libsSdk + '/jc304_kit'
final def JC305 = libsSdk + '/jc305u1_kit'
final def JC305u2 = libsSdk + '/jc305u2_kit'
final def JC305u3 = libsSdk + '/jc305u3_kit'
final def JC310b43 = libsSdk + '/jc310b43_kit'

// Which JavaCard SDK to use - select
// In order to compile JC222 and lower you have to have Java SDK <= 1.8
// For more info on JavaCard vs JDK version requirements inspect:
//   https://github.com/martinpaljak/ant-javacard/wiki/Version-compatibility
//
// JC310b43 supports building also for lower versions (cap.targetsdk).
// If another SDK is selected, please comment the cap.targetsdk setting.
final def JC_SELECTED = JC310b43

javacard {
    def MONOID_KEYSTORE_PACKAGE_AID = 'f0:6d:6f:6e:6f:69:64:00'
    def MONOID_KEYSTORE_AID = MONOID_KEYSTORE_PACKAGE_AID + ':01'

    def MONOID_PACKAGE_AID = 'f0:6d:6f:6e:6f:69:64:01'
    def MONOID_AID = MONOID_PACKAGE_AID + ':01'

    config {
        jckit JC_SELECTED

        debugGpPro true
        addImplicitJcardSim false
        addImplicitJcardSimJunit false

        cap {
            packageName 'monoidkeystore'
            version '0.1'
            aid MONOID_KEYSTORE_PACKAGE_AID
            output 'monoidkeystore.cap'

            targetsdk JC305

            sources 'src/main/java/monoidkeystore'

            applet {
                className 'monoidkeystore.MonoidKeystoreApplet'
                aid MONOID_KEYSTORE_AID
            }
        }

        cap {
            packageName 'monoid'
            version '0.1'
            aid MONOID_PACKAGE_AID
            output 'monoid.cap'

            targetsdk JC305

            sources 'src/main/java/monoid'

            applet {
                className 'monoid.MonoidApplet'
                aid MONOID_AID
            }

            dependencies {
                local {
                    exps "${buildDir}/javacard/monoidkeystore.exp"
                    jar "${buildDir}/javacard/monoidkeystore.exp/monoidkeystore.jar"
                }
            }
        }
    }

    scripts {
        script {
            name 'select'
            apdu '00:a4:04:00:09:' + MONOID_AID + ':00'
        }

        script {
            name 'hello'
            apdu '00:90:00:00'
        }

        task {
            name 'hello'
            scripts 'select', 'hello'
        }
    }
}
